ACTOR Glock21 : Weapon
{
    Inventory.PickupSound "misc/w_pkup"
    Inventory.PickupMessage "You got the Glock 21!"
    Obituary "%o was capped by %k's Glock."
    Weapon.AmmoType "Clip"
    Weapon.AmmoGive 20
    Weapon.AmmoUse 1
    Weapon.SlotNumber 2
    Weapon.Kickback 75
    Weapon.UpSound "weapons/glockup"
    AttackSound "weapons/glockfire"
    Scale 0.5
    States
    {
    Spawn:
        LIBG X -1
        Stop
    Ready:
        LIBG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        Loop
    Deselect:
        LIBG A 1 A_Lower(12)
        Loop
    Select:
        LIBG A 1 A_Raise(12)
        Loop
    Fire:
        LIBF A 0 A_GunFlash
        LIBF A 0 A_FireBullets(2,3,1,7,"BulletPuff",1)
        LIBF A 1 BRIGHT
        LIBG DCBA 1
        LIBG A 4
        LIBF A 0 A_CheckforReload(12,"Ready")
        Goto DryFire
    DryFire:
        LIBG A 12 A_PlaySound("weapons/glockdry")
        Goto Reload+1
    Reload:
        TNT1 A 0 A_CheckforReload(1,"Ready",true)
        LIBG A 1 A_PlaySound("weapons/glockout")
        LIBM AB 1
        LIBR ABC 2
        LIBR D 0 A_PlaySound("weapons/glockin")
        LIBR D 2 A_ResetReloadCounter
        LIBR EF 1
        LIBG A 3
        Goto Ready
    Flash:
        TNT1 A 2 A_Light2
        TNT1 A 1 A_Light1
        TNT1 A 0 A_Light0
        Stop
    }
}