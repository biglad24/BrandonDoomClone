ACTOR Deviation : Weapon replaces RocketLauncher
{
   Inventory.PickupSound "misc/w_pkup"
   Inventory.PickupMessage "You got the ''Deviation'' Dumb Missile Launcher!"
   Weapon.UpSound "weapons/gemiup"
   Weapon.AmmoType "RocketAmmo"
   Weapon.AmmoGive 6
   Weapon.AmmoUse 1
   Weapon.SlotNumber 5
   Scale 0.85
   States
   {
   Spawn:
      RNPR A -1
      LOOP
   Ready: 
      RPRL A 1 A_WeaponReady
      LOOP
   Deselect: 
      RPRL A 1 A_Lower
      Loop
   Select: 
      RPRL A 1 A_Raise
      Loop
   Fire:
      RPRL A 0 A_PlaySoundEx("weapons/gemifire","Auto")
      RPRL A 0 A_GunFlash
      RPRL BC 1 Bright
      RPRL C 0 A_FireCustomMissile("DumbRocket",random(3,-6),1,15,random(5,-5))
      RPRL DEF 1 Bright
      RPRL A 3
      Goto Ready
   Flash:
      TNT1 A 5 A_Light2
      TNT1 A 3 A_Light1
      TNT1 A 0 A_Light0
      Stop
   }
}

ACTOR DumbRocket : Rocket
{
 Speed 30
 -RANDOMIZE
 -ROCKETTRAIL
 SeeSound ""
 States
 {
 Spawn:
 TNT1 A 0 A_SeekerMissile(2,2,SMF_LOOK,128,10)
 TNT1 A 0 A_ChangeVelocity (1, frandom(-3, 3), frandom(-2, 2), 1)
 MISL A 1 Bright A_SpawnItemEx("DumbTrail", 0, random(2,-2), random(2,-2), 0, 0, 0, 0, 128, 0)
 Loop
 Death:
 TNT1 A 0 A_Explode
 TNT1 AAAAAAAAAA 0 bright A_SpawnItemEx("Smoke", 0, 0, 0, random(2,-2), random(2,-2), 1+random(1,-3), 0, 128, 0)
 TNT1 AAAA 0 bright A_SpawnItemEx("Boomy", 0, random(2,-2), random(2,-2), 0, 0, 1, 0, 128, 0)
 TNT1 A 1
 Stop
 }
}

ACTOR Smoke
{ 
    Scale 0.5
    Alpha 0.55
    RenderStyle Add
    +NOCLIP
    +NOGRAVITY
    States
    {
    Spawn:
      SMOK ABCDEFGHIJKLMNOPQ 1 BRIGHT
      Stop
    }
}

ACTOR Boomy
{ 
    Scale 1
    Alpha 0.75
    RenderStyle Add
    +NOCLIP
    +NOGRAVITY
    States
    {
    Spawn:
      TNT1 A 0 A_Jump(255,"Spawn1","Spawn2")
    Spawn1:    
      EXP1 ABCDEFGHIJKLMNOP 1 BRIGHT
      Stop
    Spawn2:    
      EXP2 ABCDEFGHIJKLMNOP 1 BRIGHT
      Stop
    }
}

ACTOR DumbTrail : Smoke
{ 
    Scale 0.175
    Alpha 0.3
}
