actor M79 : DoomWeapon
{
  Weapon.AmmoUse 1
  Weapon.AmmoGive 4
  Weapon.AmmoType "M79Ammo"
  Weapon.SlotNumber 5
  Inventory.PickupMessage "You got the M79 Grenade Launcher,Make it Rain Zombie Parts!"
  Obituary "%o was Thumped by %k's M79."
  Decal Scorch
  States
  {
  Ready:
    M79G A 1 A_WeaponReady
    Loop
  Deselect:
    M79G A 1 A_Lower
    Loop
  Select:
    M79G A 1 A_Raise
    Loop
  Fire:
    TNT1 A 0 A_PlayWeaponSound("M79/Fire")
    TNT1 A 0 Bright A_FireCustomMissile("M79Grenade",0,1,8,8,0)
    M79G BCD 2
    TNT1 A 0 Bright A_CheckReload
    M79G EFGHIJK 1
    TNT1 A 2 A_PlayWeaponSound("M79/Open")
    TNT1 A 10
    TNT1 A 0 A_PlayWeaponSound("M79/S-Out")
    TNT1 A 10
    TNT1 A 0 A_PlayWeaponSound("M79/S-In")
    TNT1 A 10
    TNT1 A 0 A_PlayWeaponSound("M79/Close")
    TNT1 A 10
    M79G KJIHGFE 1
    M79G A 2 A_ReFire
    Goto Ready
  Spawn:
    M79P A -1
    Stop
  }
}

ACTOR M79Grenade
{
  Radius 11
  Height 8
  Speed 50
  Damage 35
  Projectile
  +RANDOMIZE
  +DEHEXPLOSION
  +ROCKETTRAIL
  -NOGRAVITY
  Gravity 0.4
  Decal Scorch
  DeathSound "M79/BoomR"
  Obituary "%o Was Thumped by %k's M79."
  States
  {
  Spawn:
    GRND A 1 bright
    loop
  Death:
    MISL B 0 bright A_ChangeFlag("NOGRAVITY",1)
    MISL B 0 bright A_PlaySound("weapons/rocklx")
    MISL B 8 bright A_Explode
    MISL C 6 bright
    MISL D 4 bright
    stop
  }
}

ACTOR M79Ammo : Ammo
{
  Inventory.PickupMessage "Picked up a M79 Round."
  Inventory.Amount 1
  Inventory.MaxAmount 25
  Ammo.BackpackAmount 5
  Ammo.BackpackMaxAmount 50
  Inventory.Icon "M79AB0"
  States
  {
  Spawn:
    M79A A -1
    stop
  }
}

ACTOR M79AmmoBox : M79Ammo
{
  Inventory.PickupMessage "Picked up a box of M79 Rounds."
  Inventory.Amount 5
  States
  {
  Spawn:
    M79A B -1
    Stop
  }
}
