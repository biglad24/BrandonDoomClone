ACTOR LighteningRod : HereticWeapon
{
  Game Heretic
  Weapon.SelectionOrder 1600
  Weapon.AmmoUse 1
  Weapon.AmmoGive 50
  Weapon.AmmoType "BlasterAmmo"
  Weapon.SisterWeapon "LighteningRodPowered"
  Inventory.PickupMessage "Lightening Rod"
  weapon.kickback 4
  Obituary "%o got shocked by %k's Lightening Rod"
  
  States
  {
  
  Spawn:
    WROD A 16
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 4 A_Jump(64, 1)
    WROD A 0 A_Jump(128, "SpawnLUP") 
    WROD A 0 A_Jump(255, "SpawnLDOWN") 
    Loop
  SpawnLUP:
    WROD B 0 A_PlaySound("RodHit")
    WROD BCDEFG 3 BRIGHT
    goto Spawn
  SpawnLDOWN:
    WROD G 0 A_PlaySound("RodHit")
    WROD GFEDCB 3 BRIGHT
    goto Spawn
  Ready:
    LROD A 1 A_WeaponReady
    LROD A 0 A_JUMP(250,"Ready")
    LROD A 0 A_PlaySound("RodHit")
    LROD BBBBCCCCDDDDEEEE 1 BRIGHT A_WeaponReady
    LROD A 1 A_GiveInventory("BlasterAmmo",1)
    LROD AAAAAAAAA 1 A_WeaponReady
    Loop
  Deselect:
    LROD A 1 A_Lower
    Loop
  Select:
    LROD A 1 A_Raise
    Loop
  Fire:
    LROD F 3 BRIGHT A_GunFlash
  Hold:
    LROD G 3 BRIGHT A_FireCustomMissile ("LighteningBolt", 0, 1, random(6,12))
    LROD H 3 BRIGHT A_GunFlash
    LROD I 3 BRIGHT A_FireCustomMissile ("LighteningBolt", 0, 1, random(-3,3))
    LROD J 3 BRIGHT A_GunFlash
    LROD K 3 BRIGHT A_FireCustomMissile ("LighteningBolt", 0, 1, random(-12,-6))
    LROD J 3 BRIGHT A_GunFlash
    LROD I 3 BRIGHT A_FireCustomMissile ("LighteningBolt", 0, 1, random(-3,3))
    LROD F 3 BRIGHT A_ReFire
    goto Ready
  Flash:
    TNT1 A 2 A_PlaySound("RodHit")
    TNT1 A 1 A_Light(random(1,2))
    TNT1 A 0 A_Light(0)
    stop
  }
}

actor LighteningBolt
{
  height 8
  radius 4
  damage 3
  speed 35
  seesound "misc/shot"
  deathsound "misc/shotx"
  DamageType "Lightening"
  PROJECTILE
  -ACTIVATEIMPACT 
  -ACTIVATEPCROSS
  scale 0.5
  
  states
  {
  Spawn:
    FX17 FG 4
    loop
  Death:
    FX17 H 5 A_PlaySound("RodShoot")
    FX17 IJKL 5 A_RadiusThrust(32.0,32.0,0)
    stop
  }
}

ACTOR LighteningRodPowered : LighteningRod
{
  Game Heretic
  Weapon.SelectionOrder 1600
  Weapon.AmmoUse 10
  Weapon.AmmoGive 50
  Weapon.AmmoType "BlasterAmmo"
  Weapon.SisterWeapon "LighteningRod"
  Inventory.PickupMessage "Lightening Rod"
  weapon.kickback 4
  +WEAPON.POWERED_UP
  Obituary "%o gave %k some electroshock therapy"
  
  States
  {
  Fire:
    LROD F 3 BRIGHT A_GunFlash
  Hold:
    LROD M 3 BRIGHT A_GunFlash
    LROD N 3 BRIGHT 
    LROD O 3 BRIGHT 
    LROD P 3 BRIGHT 
    LROD P 0 BRIGHT A_FireCustomMissile ("LighteningBolt2", 0, 1, -96)
    LROD P 0 BRIGHT A_FireCustomMissile ("LighteningBolt2", 0, 0, -64)
    LROD P 0 BRIGHT A_FireCustomMissile ("LighteningBolt2", 0, 0, -32)
    LROD P 0 BRIGHT A_FireCustomMissile ("LighteningBolt2", 0, 0,  0)
    LROD P 0 BRIGHT A_FireCustomMissile ("LighteningBolt2", 0, 0,  32)
    LROD P 0 BRIGHT A_FireCustomMissile ("LighteningBolt2", 0, 0,  64)
    LROD P 0 BRIGHT A_FireCustomMissile ("LighteningBolt2", 0, 0,  96)
    LROD M 6 
    goto Ready
  Flash:
    TNT1 A 1 A_PlaySound("RodHit")
    TNT1 A 2 A_Light(random(1,2))
    TNT1 A 2 A_Light(random(1,3))
    TNT1 A 2 A_Light(random(2,4))
    TNT1 A 2 A_Light(random(3,5))
    TNT1 A 0 A_Light(0)
    stop
  }
}

actor LighteningBolt2
{
  height 8
  radius 4
  damage 1
  speed 15
  seesound "misc/shot"
  deathsound "misc/shotx"
  DamageType "Lightening"
  PROJECTILE
  +RIPPER
  -ACTIVATEIMPACT 
  -ACTIVATEPCROSS
  +SEEKERMISSILE
  scale 1
  
  states
  {
  Spawn:
    FX17 G 0
    FX17 G 0 A_Jump(128, "Bounce")
  Seek:
    FX17 F 0 A_LookEx(LOF_NOSOUNDCHECK, 512, 4096, 0, 360, 1)
    FX17 FFFF 1 A_SeekerMissile(360,10,SMF_LOOK + SMF_PRECISE, 255, 20)
    FX17 GGGG 1 A_SeekerMissile(360,10,SMF_LOOK + SMF_PRECISE, 255, 20)
    FX17 G 0 A_Jump(32, "Bounce")
    loop
  Bounce:
    FX17 FFFF 1 A_Weave (random(0,2), random(0,2), 3.0, 3.0)
    FX17 GGGG 1 A_Weave (random(0,2), random(0,2), 3.0, 3.0)
    FX17 G 0 A_Jump(32, "Seek")
    loop
  Death:
    FX17 H 5 A_PlaySound("RodShoot")
    FX17 IJKL 5 A_RadiusThrust(32.0,32.0,0)
    stop
  }
}

